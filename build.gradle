plugins {
    id 'idea'
    id 'java'
    id 'groovy'
    id 'application'
    id 'codenarc'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

group 'lt.vinted.homework'
version '1.0-snapshot'

sourceSets {
    main {
        java.srcDirs = []
        groovy.srcDirs = ['src/main/groovy']
    }
    test {
        java.srcDirs = []
        groovy.srcDirs = ['src/test/groovy']
    }

    itest {
        java.srcDirs = []
        groovy.srcDirs = ['src/itest/groovy']
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    itestCompile.extendsFrom testCompile, localCompile
    itestRuntime.extendsFrom testRuntime
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.4.15'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.6'
}

idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
        vcs = 'Git'
    }

    module {
        testSourceDirs += sourceSets.itest.allSource.srcDirs
    }
}

def distName = 'discounts'
shadowJar {
    baseName = distName
    classifier = null
    version = null
    mainClassName = 'lt.vinted.homework.App'
}

task copyBin(type: Copy) {
    from "build/libs/${distName}.jar"
    into "${project.projectDir}/"
}

copyBin.mustRunAfter shadowJar
build.dependsOn copyBin

codenarc {
    config = resources.text.fromFile("${project.projectDir}/gradle/codenarc/codenarc.groovy")
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

task itest(type: Test, description: 'Runs the integration tests.') {
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    jvmArgs = ["-Duser.language=en -Duser.country=US"]
    mustRunAfter test
}
